pkgname="carbonio-proxy"
pkgver="4.10.0"
pkgrel="1"
pkgdesc="An open-source, community-driven email server"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
copyright=(
  "2022-2024, Zextras <https://www.zextras.com>"
  "2022, Synacor, Inc."
)
license=(
  "GPL-2.0-only"
)
url="https://github.com/zextras"
section="mail"
priority="optional"
depends__apt=(
  "carbonio-certbot"
  "carbonio-core"
  "carbonio-nginx"
  "carbonio-prometheus-nginx-exporter"
  "sqlite3"
)
depends__yum=(
  "carbonio-certbot"
  "carbonio-core"
  "carbonio-nginx"
  "carbonio-prometheus-nginx-exporter"
  "sqlite"
)
source=(
  "211-carbonio-clamav-signature-provider-setup.sh"
  "311-${pkgname}-setup.sh"
  "${pkgname}-sidecar.service"
  "${pkgname}-sidecar-legacy.service"
  "${pkgname}.hcl"
  "${pkgname}.target"
  "${pkgname}.sh"
  "carbonio-clamav-signature-provider-sidecar.service"
  "carbonio-clamav-signature-provider-sidecar-legacy.service"
  "carbonio-clamav-signature-provider.hcl"
  "carbonio-clamav-signature-provider"
  "intentions-clamav-signature-provider.json"
  "policies-clamav-signature-provider.json"
  "policies-proxy.json"
  "service-protocol-clamav-signature-provider.json"
  "service-protocol-proxy.json"
  "systemd-sysuser.conf"
  "systemd-tmpfile.conf"
)

sha256sums=(
  'cbd42efea9c34bd8cd661a7f9a4902ee86a3ba2b07db5c2be07dc36ab894df7a'
  'bb536a6e911924cde7438acdfaffaf88efcc070089319bad5dc9c68f5e7a33e3'
  'e7823ac7925801b9d8f58c30e9767a1f76187216412a04b249ce19c9f168aef1'
  '54d1260932b8113d3ac13a7a2002b126398b1c971ef08025417bc0ad2a6933c7'
  'd503d1f4b4d966999a546b00523a746ba2277495a840135e1878f7855c00114f'
  '7e5765f837ccbde44c598c80be3576bea469d041b318ed558a67e7d1c15d9948'
  '98e5fccf13b8ca8691c19495e5e496d1daa24a5962178e3f7b86706507c56e55'
  'c420b181199c739d540ed967ead2c2978488523c25c8e90d1f91213939e1edd3'
  '9e6d5473036305706c9b25a7c13d690caf56ea83755dca007f9414edbc657c6c'
  'e31069c73e7326c68188381fd485866c8d083466a576ee9141b9fdbefbb2c157'
  '9cb0f1b78f9a93dc8c05d7dc3db1514ef9af5a4f165416a497b543a3d620438d'
  '5b9d7a1598959eec4d81994d8a3d9c912d77f0b75de84529d3b65a19ee13c724'
  '8eedc2664b9d837fd7527410414d6d3b29ddc5acebec9b8a7bb4d35b804151bc'
  '5bf2b4598abd0dbbf7bd9ff94858f2c73c0f88504e9f83e0a3952c5e52b7a968'
  'a015381620f2a4efeb33310e9b8fec40dbf71c3683164806b4f92000f786c18f'
  'c8655bcb535fac75689bf64434d03b4f0365f33d4d2d23b2a62e6a9b2da45fba'
  '2819b3c6a29f00be525dde989c536e141a26bfa9c6d9b6aa4cbb62b3252ce2db'
  'cb0d5c96f4d9488f84df1f15c193d90f2513432c78dbfd5b49ef27438a5f7cec'
)

_package() {
  cd "$(dirname "$(find / -name "yap.json" -print -quit)")"

  install -D target/proxyconfgen.jar \
    "${pkgdir}/opt/zextras/lib/jars/proxyconfgen.jar"

  cd "${srcdir}/.."
  # start install proxy service registration files for consul
  install -Dm755 "${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm644 "policies-proxy.json" \
    "${pkgdir}/etc/carbonio/proxy/service-discover/policies.json"

  install -Dm644 "${pkgname}.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"

  install -Dm644 "service-protocol-proxy.json" \
    "${pkgdir}/etc/carbonio/proxy/service-discover/service-protocol.json"

  install -Dm755 "311-${pkgname}-setup.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/311-${pkgname}-setup.sh"
  # end install proxy service registration files for consul

  # start install clamav-signature-provider service registration files for consul
  install -Dm755 "carbonio-clamav-signature-provider" \
    "${pkgdir}/usr/bin/carbonio-clamav-signature-provider"

  install -Dm644 "policies-clamav-signature-provider.json" \
    "${pkgdir}/etc/carbonio/clamav-signature-provider/service-discover/policies.json"

  install -Dm644 "carbonio-clamav-signature-provider.hcl" \
    "${pkgdir}/etc/zextras/service-discover/carbonio-clamav-signature-provider.hcl"

  install -Dm644 "intentions-clamav-signature-provider.json" \
    "${pkgdir}/etc/carbonio/clamav-signature-provider/service-discover/intentions.json"

  install -Dm644 "service-protocol-clamav-signature-provider.json" \
    "${pkgdir}/etc/carbonio/clamav-signature-provider/service-discover/service-protocol.json"

  install -Dm755 "211-carbonio-clamav-signature-provider-setup.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/211-carbonio-clamav-signature-provider-setup.sh"
  # end install clamav-signature-provider service registration files for consul

  # start install template and errors files
  mkdir -p "${pkgdir}/opt/zextras/conf/nginx/includes/"
  mkdir -p "${pkgdir}/opt/zextras/conf/nginx/templates/"
  mkdir -p "${pkgdir}/opt/zextras/conf/nginx/templates_custom/"
  mkdir -p "${pkgdir}/opt/zextras/conf/nginx/extensions/"
  mkdir -p "${pkgdir}/opt/zextras/data/nginx/html/"

  cp conf/nginx/templates/*.template \
    "${pkgdir}/opt/zextras/conf/nginx/templates/"
  cp conf/nginx/errors/*.html \
    "${pkgdir}/opt/zextras/data/nginx/html/"
  # end install template and errors files

  # systemd tmpfiles.d
  install -Dm644 "${srcdir}/systemd-tmpfile.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  # systemd sysusers.d
  install -Dm644 "${srcdir}/systemd-sysuser.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}

_package_systemd() {
  # systemd units and target
  mkdir -p "${pkgdir}/usr/lib/systemd/system/carbonio.target.wants"
  install -Dm 644 "${pkgname}.target" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.target"
  ln -sf "/usr/lib/systemd/system/${pkgname}.target" \
    "${pkgdir}/usr/lib/systemd/system/carbonio.target.wants/${pkgname}.target"
}

_postinst() {
  # Create users and groups via systemd-sysusers
  systemd-sysusers /usr/lib/sysusers.d/carbonio-proxy.conf >/dev/null 2>&1 || :
  # Create directories and set ownership via tmpfiles.d
  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-proxy.conf >/dev/null 2>&1 || :

  # Note: chmod resets capabilities, so setcap must run after chmod
  chown root:zextras /opt/zextras/common/sbin/nginx
  chmod 750 /opt/zextras/common/sbin/nginx
  setcap CAP_NET_BIND_SERVICE=+ep /opt/zextras/common/sbin/nginx

  echo "======================================================"
  echo "Carbonio Proxy installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"

  echo "======================================================"
  echo "Carbonio ClamAV signature provider installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"
}

_postinst_legacy() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-proxy-sidecar.service &>/dev/null || :
    systemctl enable carbonio-clamav-signature-provider-sidecar.service &>/dev/null || :
  fi
}

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-proxy.target &>/dev/null || :
    systemctl enable carbonio-proxy-sidecar.service &>/dev/null || :
    systemctl enable carbonio-clamav-signature-provider-sidecar.service &>/dev/null || :
  fi
}

_prerm_legacy() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-clamav-signature-provider-sidecar.service &>/dev/null || :
    systemctl --no-reload disable carbonio-proxy-sidecar.service &>/dev/null || :
    systemctl stop carbonio-clamav-signature-provider-sidecar.service &>/dev/null || :
    systemctl stop carbonio-proxy-sidecar.service &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-clamav-signature-provider-sidecar.service &>/dev/null || :
    systemctl --no-reload disable --now carbonio-proxy.target &>/dev/null || :
    systemctl --no-reload disable --now carbonio-proxy-sidecar.service &>/dev/null || :
  fi
}

package() {
  _package
  _package_systemd

  install -Dm644 "${srcdir}/../${pkgname}-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"
  install -Dm644 "${srcdir}/../carbonio-clamav-signature-provider-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-clamav-signature-provider-sidecar.service"
}

package__rocky_8() {
  _package

  install -Dm644 "${srcdir}/../${pkgname}-sidecar-legacy.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"
  install -Dm644 "${srcdir}/../carbonio-clamav-signature-provider-sidecar-legacy.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-clamav-signature-provider-sidecar.service"
}

package__rocky_9() {
  _package
  _package_systemd

  install -Dm644 "${srcdir}/../${pkgname}-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"
  install -Dm644 "${srcdir}/../carbonio-clamav-signature-provider-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-clamav-signature-provider-sidecar.service"
}

package__ubuntu_jammy() {
  _package

  install -Dm644 "${srcdir}/../${pkgname}-sidecar-legacy.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"
  install -Dm644 "${srcdir}/../carbonio-clamav-signature-provider-sidecar-legacy.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-clamav-signature-provider-sidecar.service"
}

package__ubuntu_noble() {
  _package
  _package_systemd

  install -Dm644 "${srcdir}/../${pkgname}-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"
  install -Dm644 "${srcdir}/../carbonio-clamav-signature-provider-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-clamav-signature-provider-sidecar.service"
}

postinst__rocky_8() {
  _postinst
  _postinst_legacy
}

postinst__rocky_9() {
  _postinst
  _postinst_systemd
}

postinst__ubuntu_jammy() {
  _postinst
  _postinst_legacy
}

postinst__ubuntu_noble() {
  _postinst
  _postinst_systemd
}

prerm__rocky_8() {
  _prerm_legacy
}

prerm__rocky_9() {
  _prerm_systemd
}

prerm__ubuntu_jammy() {
  _prerm_legacy
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

postrm() {
  rm -f /etc/carbonio/proxy/service-discover/token
  rm -f /etc/carbonio/clamav-signature-provider/service-discover/token

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
  fi
}
